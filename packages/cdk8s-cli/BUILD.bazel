package(default_visibility = ["//visibility:public"])

load("//:packages.bzl", "CDK_PACKAGE_VERSION", "JSII_PACKAGE_VERSION")
load("@build_bazel_rules_nodejs//:index.bzl", "copy_to_bin")
load("//tools:defaults.bzl", "jest_test", "package_json", "pkg_npm", "ts_library")

MODULE_NAME = "cdk8s-cli"

package_json(
    name = "package_json",
    package_name = MODULE_NAME,
    bin = {
        "cdk8s": "bin/cdk8s",
    },
    desc = "CDK for Kubernetes CLI",
    experimental = True,
    deps = {
        "@aws-cdk/core": CDK_PACKAGE_VERSION,
        "@aws-cdk/cx-api": CDK_PACKAGE_VERSION,
        "@types/node": "13.7.0",
        "cdk8s": "0.0.0-PLACEHOLDER",
        "codemaker": "^0.22.0",
        "fs-extra": "^8.1.0",
        "jsii": JSII_PACKAGE_VERSION,
        "jsii-pacmak": JSII_PACKAGE_VERSION,
        "sscaff": "^1.2.0",
        "yaml": "^1.7.2",
        "yargs": "^15.1.0",
    },
)

ts_library(
    name = "cdk8s_cli",
    srcs = glob(
        [
            "lib/**/*.ts",
            "bin/**/*.ts",
        ],
        exclude = ["test/**"],
    ),
    module_name = MODULE_NAME,
    deps = [
        "//packages/cdk8s:lib",
        "@npm//@aws-cdk/core",
        "@npm//@types/fs-extra",
        "@npm//@types/json-schema",
        "@npm//@types/yaml",
        "@npm//@types/yargs",
        "@npm//codemaker",
        "@npm//sscaff",
    ],
)

ts_library(
    name = "tests",
    testonly = True,
    srcs = glob(["test/**/*.ts"]),
    deps = [
        ":cdk8s_cli",
        "@npm//@types/fs-extra",
        "@npm//@types/json-schema",
        "@npm//codemaker",
    ],
)

jest_test(
    name = "test",
    srcs = glob(["**/*.snap"]),
    lib = ":tests",
    deps = [
        ":cdk8s_cli",
        "//packages/cdk8s:lib",
        "@npm//@aws-cdk/core",
        "@npm//codemaker",
        "@npm//jsii",
        "@npm//tslib",
    ],
)

filegroup(
    name = "templates",
    srcs = glob(["templates/**/*"]),
)

copy_to_bin(
    name = "copy_templates",
    srcs = [":templates"],
)

# Creates the @awslabs/cdk8s-cli package published to npm.
pkg_npm(
    name = "pkg",
    srcs = [
        "README.md",
        "bin/cdk8s",
        ":templates",
    ],
    tags = ["release"],
    deps = [
        ":cdk8s_cli",
        ":package.json",
    ],
)

# Exported to be referenced as entry_point of the nodejs_binary
exports_files(["bin/cdk8s"])
